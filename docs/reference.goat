/*

Copyright (C) 2017, 2018 Ivan Kniazkov

This file is part of interpreter of programming language
codenamed "Goat" ("Goat interpreter").

Goat interpreter is free software: you can redistribute it and/or
modify it under the terms of the GNU General Public License as published
by the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Goat interpreter is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License along
with Goat interpreter.  If not, see <http://www.gnu.org/licenses/>.

*/

import "xml.goat";

/*
	Read XML file
*/
var file = open("reference.xml", File.Mode.READ);
debug;
file.seek(0, File.Origin.END);
var len = file.position();
file.seek(0, File.Origin.BEGIN);
var raw = file.read(len);
var xml = raw.decodeUTF8();

/*
	Parse XML and generate HTML
*/
var parseSection = $(src, level, prefix, dst) {
	var i, l = src.content.length();
	var k = 1;
	for (i = 0; i < l; i++) {
		var child = src.content[i];
		if (child.instanceOf(XML.Element)) {
			if (child.tag == "section") {
				var title = new XML.Element("div", prefix + k + ". " + child.attrib.name);
				title.attrib["style"] = "font-size: " + level2FontSize(level) + "pt; font-weight: bold;";
				dst.content.push(title);
				var section = new XML.Element("div");
				dst.content.push(section);
				section.attrib["style"] = "margin-left: 30px;";
				parseSection(child, level + 1, prefix + k + '.', section);
				k++;
			}
			else if (child.tag == "p") {
				var para = new XML.Element("div", "" + child.content[0]);
				dst.content.push(para);
			}
			else if (child.tag == "url") {
				var href = new XML.Element("a");
				href.attrib["href"] = child.content[0];
				href.content.push("" + child.content[0]);
				dst.content.push(href);
			}
		}
		else if (child.instanceOf(String)) {
			dst.content.push(child);
		}
	}
};

var level2FontSize = $(level) {
	switch(level) {
		case 0:
			return 18;
		case 1:
			return 16;
		case 2:
			return 14;
		default:
			return 12;
	}
};

var root = null;
try {
	var html = new XML.Element("html");
	var head = new XML.Element("head");
	html.content.push(head);
	var title = new XML.Element("title", "The Goat Programming Language Reference");
	head.content.push(title);
	var font = new XML.Element("link");
	head.content.push(font);
	font.attrib["href"] = "https://fonts.googleapis.com/css?family=Barlow+Semi+Condensed";
	font.attrib["rel"] = "stylesheet";
	var style = new XML.Element("style");
	head.content.push(style);
	style.content.push("body { font-family: \'Barlow Semi Condensed\', sans-serif; font-size: 12 pt}");
	var body = new XML.Element("body");
	html.content.push(body);
	root = XML.parse(xml);
	parseSection(root, 0, "", body);
	print(html.toString("  "));
} catch(e) {
	if (e.instanceOf(XML.Error))
		print("error, position: " + e.index + ", type: " + e.type);
	else
		print(e.trace);
}
